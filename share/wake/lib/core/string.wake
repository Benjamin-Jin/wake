# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# String methods

global def cat strings =
  def catopen    = prim "catopen"
  def catclose c = prim "catclose"
  def catadd c s = prim "catadd"
  catclose (foldl catadd catopen strings)

global def catWith separator strings =
  if strings.empty then "" else
    def inserted = foldr (separator, _, _) Nil strings
    cat inserted.tail

# String => List String of codepoints
global def explode str = prim "explode"

# String <=> Integer type conversion
global def strbase base n = prim "str" # int -> string
global def intbase base s = prim "int" # string -> int
global def str n = strbase 10 n
global def int s = intbase 0 s

# String <=> Integer Unicode conversion
global def integerToUnicode i = prim "code2str"
global def unicodeToInteger s = prim "str2code"

# String <=> Integer Binary conversion (Warning: may create invalid Unicode)
global def integerToByte i = prim "bin2str"
global def byteToInteger s = prim "str2bin"

# Version of wake
global def version = prim "version"

# Unicode normalization methods
global def sNFC x = prim "sNFC"
global def sNFKC x = prim "sNFKC"
global def scaseNFKC x = prim "scaseNFKC"

# Unicode NFC string comparison Ç == C+◌̧
global def scmpNFC x y = scmpRaw (sNFC x) (sNFC y)
# Unicode NFKC string comparison (¼i⁹ = 1/4i9)
global def scmpNFKC x y = scmpRaw (sNFKC x) (sNFKC y)
# Unicode case insensitive NFKC comparison
global def scasecmpNFKC x y = scmpRaw (scaseNFKC x) (scaseNFKC y)
# Raw binary string comparison; no normalization performed
global def scmpRaw x y = prim "scmp"

# NFKC order (fancy format removed) -- secure default
# This is the string order you should probably be using
global def x <=>~y = scmpNFKC x y
global def x <~  y = lt scmpNFKC x y
global def x >~  y = y <~ x
global def x >=~ y = ! x <~ y
global def x <=~ y = ! y <~ x

global def x ==~ y = eq scmpNFKC x y
global def x !=~ y = ! x ==~ y

# Case insensitive order (^ = upper-case)
global def x <=>^y = scasecmpNFKC x y
global def x <^  y = lt scasecmpNFKC x y
global def x >^  y = y <^ x
global def x >=^ y = ! x <^ y
global def x <=^ y = ! y <^ x

global def x ==^ y = eq scasecmpNFKC x y
global def x !=^ y = ! x ==^ y

# Raw binary string order
# Only use this for non-textual data
global def x <=>*y = scmpRaw x y
global def x <*  y = lt scmpRaw x y
global def x >*  y = y <* x
global def x >=* y = ! x <* y
global def x <=* y = ! y <* x

global def x ==* y = eq scmpRaw x y
global def x !=* y = ! x ==* y
