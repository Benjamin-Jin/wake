# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# String methods

# !!! the 'helper' here is to force the list to exist before lcat is called
global def cat listString =
  def doit x = prim "lcat"
  def helper = match _
    "", t = helper t
    _, t = helper t
    Nil = doit listString
  helper listString

global def catWith separator strings =
  if strings.empty then "" else
    def inserted = foldr (separator, _, _) Nil strings
    cat inserted.tail

# String => List String of codepoints
global def explode str = prim "explode"

# String <=> Integer type conversion
global def strbase base n = prim "str" # int -> string
global def intbase base s =
  def p b s = prim "int" # string -> int
  p base s | head
global def str n = strbase 10 n
global def int s = intbase 0 s

# String <=> Integer Unicode conversion
global def integerToUnicode i = prim "code2str"
global def unicodeToInteger s = prim "str2code"

# String <=> Integer Binary conversion (Warning: may create invalid Unicode)
global def integerToByte i = prim "bin2str"
global def byteToInteger s = prim "str2bin"

# Version of wake
global def version = prim "version"

# Unicode normalization methods
global def unicodeCanonical  x = prim "sNFC"
global def unicodeIdentifier x = prim "sNFKC"
global def unicodeLowercase  x = prim "scaseNFKC"

# Unicode NFC string comparison Ç == C+◌̧
global def scmpCanonical x y = scmp (unicodeCanonical x) (unicodeCanonical y)
# Unicode NFKC string comparison (¼i⁹ = 1/4i9)
global def scmpIdentifier x y = scmp (unicodeIdentifier x) (unicodeIdentifier y)
# Unicode case insensitive NFKC comparison
global def scmpLowercase x y = scmp (unicodeLowercase x) (unicodeLowercase y)
# Raw binary string comparison; no normalization performed
global def scmp x y = prim "scmp"

# NFKC order (fancy format removed) -- secure default
# This is the string order you should probably be using
global def x <=>~y = scmpIdentifier x y

# Returns True if x is less than y.
global def x <~  y = isLT (x <=>~ y)

# Returns True if x is greater than y.
global def x >~  y = isGT (x <=>~ y)

# Returns True if x is greater than or equal to y.
global def x >=~ y = isGE (x <=>~ y)

# Returns True if x is less than or equal to y.
global def x <=~ y = isLE (x <=>~ y)

# Returns True if x is equal to y.
global def x ==~ y = isEQ (x <=>~ y)

# Returns True if x is not equal to y.
global def x !=~ y = isNE (x <=>~ y)

# Case insensitive order (^ = capitals ignored)
global def x <=>^y = scmpLowercase x y

# Returns True if x is less than y, ignoring case.
global def x <^  y = isLT (x <=>^ y)

# Returns True if x is greater than y, ignoring case.
global def x >^  y = isGT (x <=>^ y)

# Returns True if x is greater than or equal to y, ignoring case.
global def x >=^ y = isGE (x <=>^ y)

# Returns True if x is less than or equal to y, ignoring case.
global def x <=^ y = isLE (x <=>^ y)

# Returns True if x is equal to y, ignoring case.
global def x ==^ y = isEQ (x <=>^ y)

# Returns True if x is not equal to y, ignoring case.
global def x !=^ y = isNE (x <=>^ y)

# Raw binary string order
# Only use this for non-textual data
global def x <=>*y = scmp x y

# Returns True if x is less than y, in raw binary representation.
global def x <*  y = isLT (x <=>* y)

# Returns True if x is greater than y, in raw binary representation.
global def x >*  y = isGT (x <=>* y)

# Returns True if x is greater than or equal to y, in raw binary representation.
global def x >=* y = isGE (x <=>* y)

# Returns True if x is less than or equal to y, in raw binary representation.
global def x <=* y = isLE (x <=>* y)

# Returns True if x is equal to y, in raw binary representation.
global def x ==* y = isEQ (x <=>* y)

# Returns True if x is not equal to y, in raw binary representation.
global def x !=* y = isNE (x <=>* y)
