# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

global data Result pass fail =
  Pass pass
  Fail fail

global def isPass = match _
  Pass _ = True
  Fail _ = False

global def isFail = match _
  Pass _ = False
  Fail _ = True

global def getPass = match _
  Pass x = Some x
  Fail _ = None

global def getFail = match _
  Pass _ = None
  Fail x = Some x

global def getWhenFail fail = match _
  Pass a = a
  Fail _ = fail

global def rmap fn = match _
  Pass a = Pass (fn a)
  Fail f = Fail f

global def rmapFail fn = match _
  Pass a = Pass a
  Fail f = Fail (fn f)

global def panic s = prim "panic"

global def :!!! result = match result
  Pass p = p
  Fail (Error cause stack) = panic "Unexpected Error {cause}{map ("\n  from {_}") stack | cat}"

global tuple Error =
  global Cause: String
  global Stack: List String

global def makeError cause =
  def stack s = prim "stack"
  Fail (Error cause (stack Unit))
