# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Any => String conversion
global def format any = prim "format"

# These values often come from the wake command-line
# default => Normal
# -d      => Normal
# -q      => Quiet
# -v      => Verbose
# -dv     => Debug
global data LogLevel =
  Debug
  Verbose
  Normal
  Quiet

global data EchoTarget =
  Null
  Stdout
  Stderr # rendered in red if on a tty

global def logError = match _
  Quiet = Stderr
  _     = Stderr

global def logWarn = match _
  Quiet = Null
  _     = Stderr

global def logNormal = match _
  Quiet = Null
  _     = Stdout

global def logVerbose = match _
  Debug   = Stdout
  Verbose = Stdout
  _       = Null

global def logDebug = match _
  Debug = Stdout
  _     = Null

global def logNever = match _
  Debug = Null
  _     = Null

# Useful helper method for inserting a debug step into a pipeline
# def foo = "123123" | tap ("that: {_}") | int | getOrElse 0
global def tap consumerFn value =
  def _ = consumerFn value
  value

# Print the tap output to the console
global def tapLevel level formatFn value =
  def _ = printlnLevel level (formatFn value)
  value

global def tapError   = tapLevel logError
global def tapWarn    = tapLevel logWarn
global def tapNormal  = tapLevel logNormal
global def tapVerbose = tapLevel logVerbose
global def tapDebug   = tapLevel logDebug

# This is private so that users cannot make the build depend on it
def logLevel = match (prim "level")
  0 = Quiet
  1 = Normal
  2 = Verbose
  _ = Debug

# Print with a given verbosity
global def printLevel logLevelFn =
  def p i outputStr = prim "print"
  match (logLevelFn logLevel)
    Null   = (\_ Unit)
    Stdout = p 1
    Stderr = p 2

global def printlnLevel logLevelFn =
  def f = printLevel logLevelFn
  (\outputStr f "{outputStr}\n")

# Print any type to stdout; even exceptions
global def print   = printLevel logNormal
global def println = printlnLevel logNormal

# Wait for the top of a value to be made available, then run (f x)
# Eg: x = 1, 2, 3, Nil; waitOne f x waits for ',' to be known vs. Nil
global def waitOne f x =
  def wait x = prim "wait_one"
  match (wait x)
    Unit = f x

# Wait for all of a value to be made available, then run (f x)
# Eg: x = 1, 2, 3, Nil; waitAll f x waits for all the values 1-3 to be known
global def waitAll f x =
  def wait x = prim "wait_all"
  match (wait x)
    Unit = f x
