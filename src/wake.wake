# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

def version_h _ =
  def cmdline = which "git", "describe", "--tags", "--dirty", Nil
  def git = volatileJob cmdline Nil (\_ Nil)
  match git.getJobStdout
    Fail f = makeBadJob f
    Pass stdout = write "{here}/version.h" "#define VERSION {stdout}"

def pkg name = match (pkgConfig name)
  Some x = x
  None   = makeSysLib "" | editSysLibLFlags ("-l{name}", _)

global def buildWake (Pair variant clib) =
  def internalDeps = common variant, map (_ clib) (utf8proc, gopt, Nil)
  def externalDeps = map pkg ("sqlite3", "ncurses", "gmp", "re2", Nil)
  def deps = internalDeps ++ externalDeps | flattenSysLibs
  def reFiles = sources here `.*\.re`
  def headerFiles = version_h 0, deps.getSysLibHeaders ++ sources here `.*\.h`
  def cppFiles = map re2c reFiles ++ sources here `.*\.cpp`
  def compile = compileC variant deps.getSysLibCFlags headerFiles
  def objFiles = map compile cppFiles ++ deps.getSysLibObjects
  linkO variant deps.getSysLibLFlags objFiles "bin/wake"
