# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# useful build variants
global def default = Pair "native-cpp11-release" "native-c99-release"
global def debug   = Pair "native-cpp11-debug" "native-c99-debug"

def targets = buildWake, buildFuse, buildFuseDaemon, buildShim, buildPreload, buildPrelib, Nil
def allOut variant = map (_ variant) targets

global def all variant = allOut variant | findSomeFn getPathError | getOrPass "BUILT"

global def install dest =
  def libdir = simplify "{here}/share/wake/lib"
  def libfiles = sources libdir `.*`
  def binfiles = allOut default
  def releaseAs exe = cat (dest, "/", take 1 (extract `(.*)\.(.*)` exe))
  def libinstall = map (installIn dest) libfiles
  def bininstall = map (\exe installAs (releaseAs exe.getPathName) exe) binfiles
  libinstall ++ bininstall | findSomeFn getPathError | getOrPass "INSTALLED"

global def tarball Unit = match (buildAs Unit) (buildOn Unit)
  (Fail f) _ = Fail f
  _ (Fail f) = Fail f
  (Pass release) (Pass time) =
    def sed = source "src/sed-version"
    def makeRelease file =
      def in = source "{file}.in"
      def cmd = sed.getPathName, release, in.getPathName, file, Nil
      job cmd (sed, in, Nil) | getJobOutput
    def extra = map makeRelease ("debian/changelog", "wake.spec", Nil)
    def psrcs = sources "." `([^.][^/]*/)*[^.][^/]*` # ignore .foo/ files
    def srcs = psrcs | filter (! matches `(debian/|wake.spec).*` _.getPathName)
    def pub = map (_.getPathName.format) srcs | foldr (_, ",\n  ", _) Nil | cat
    def manifest = write "manifest.wake" (
      "# Generated by 'wake tarball Unit':\n",
      "publish releaseAs = '{release}', Nil\n",
      "publish releaseOn = '{time}', Nil\n",
      "publish source =\n  {pub}Nil\n",
      Nil
    ).cat
    def gnutar = which "gnutar"
    def tar = if gnutar ==* "gnutar" then which "tar" else gnutar
    def cmd =
      tar, "--mtime={time}", "--transform=s@^@wake-{release}/@",
      "--owner=0", "--group=0", "--numeric-owner", "-cJf",
      "wake_{release}.tar.xz", map getPathName (manifest, srcs) | sortBy (_<*_)
    def tarball = job cmd (manifest, srcs) | getJobOutput
    (tarball, extra) | findSomeFn getPathError | getOrPass "TARBALL"
